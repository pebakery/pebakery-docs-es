# System,RefreshAllScripts

**Alias:** `System,RescanScripts`

Escanea la ruta especificada para proyectos y scripts nuevos/modificados y los agrega al 치rbol del proyecto.

## Sintaxis

```pebakery
System,RefreshAllScripts
```

### Argumentos

Este comando no tiene argumentos

## Observaciones

Este comando se puede utilizar para reconstruir el 치rbol del proyecto cuando se agrega o modifica un script y realiza la misma operaci칩n que presionando el bot칩n **Refresh** en la ventana principal.

## Relacionado

[System,LoadAll](./LoadAll.md)

## Ejemplos

### Ejemplo 1

```pebakery
[Main]
Title=RefreshAllScripts Ejemplo
Author=Homes32
Description=Mostrar el uso del comando RefreshAllScripts.
Version=1
Level=5
Selected=True

[Interface]
BTN_Clean=Cleanup,1,8,100,60,80,25,Clean,0,True,_Clean_,True

[variables]
%myProject%=%BaseDir%\Projects\myProject\script.project
%myScript%=%BaseDir%\Projects\myProject\myScript.script

[Clean]
Echo,"Eliminando myProject Ejemplo..."
If,EXISTDIR,%BaseDir%\Projects\myProject\,DirDelete,%BaseDir%\Projects\myProject\
System,RefreshAllScripts

[process]
If,Not,ExistFile,%myProject%,FileCreateBlank,%myProject%
If,Not,ExistFile,%myScript%,FileCreateBlank,%myScript%

// Proyecto
IniWrite,%myProject%,Main,Title,myProject
IniWrite,%myProject%,Main,Author,Homes32
IniWrite,%myProject%,Main,Description,"A brand new project!"
IniWrite,%myProject%,Main,Version,1
IniWrite,%myProject%,Main,Level,5

TXTAddLine,%myProject%,"[Variables]",Append
TXTAddLine,%myProject%,"",Append
TXTAddLine,%myProject%,"[Process]",Append
TXTAddLine,%myProject%,"[Interface]",Append
TXTAddLine,%myProject%,"",Append

// Script
IniWrite,%myScript%,Main,Title,myScript
IniWrite,%myScript%,Main,Author,Homes32
IniWrite,%myScript%,Main,Description,"A brand new Script!"
IniWrite,%myScript%,Main,Version,1
IniWrite,%myScript%,Main,Level,5
IniWrite,%myScript%,Main,Selected,False

TXTAddLine,%myScript%,"[Variables]",Append
TXTAddLine,%myScript%,"",Append
TXTAddLine,%myScript%,"[Process]",Append
TXTAddLine,%myScript%,"[Interface]",Append
TXTAddLine,%myScript%,"",Append

// Ahora necesitamos llamar al siguiente comando para que nuestro nuevo proyecto y script se muestren en la ventana principal.
System,RefreshAllScripts
```
